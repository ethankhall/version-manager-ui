buildscript {
    repositories {
        jcenter()
        maven {
            url 'http://repo.ehdev.io/maven2'
        }
    }
    dependencies {
        classpath 'tech.crom.client:gradle-client:+'
    }
}

import org.gradle.api.tasks.Copy
import org.gradle.api.tasks.Exec

apply plugin: 'base'
apply plugin: 'version-manager'

versionManager {
    projectName = 'ethankhall'
    repoName = 'version-manager-ui'
}

task npmInstall(type: Exec) {
    inputs.files(file('package.json'))
    outputs.dir(file('node_modules'))
    commandLine 'npm install'.split(' ')
}

task prodBuild(type: Exec) {
    inputs.file(file('tsconfig.json'))
    inputs.file(file('webpack.config.js'))
    inputs.files(file('src'))
    inputs.files(file('typings'))
    inputs.files(file('config'))
    outputs.dir(file("dist"))

    dependsOn npmInstall
    commandLine 'npm run build:prod'.split(' ')
}

task prepairAngularPackage(type: Copy) {
    dependsOn 'prodBuild'
    from file('dist')
    into file('docker/content')
}

task buildAngularContainer(type: Exec) {
    inputs.property('version', version)
    dependsOn prepairAngularPackage
    commandLine "docker build --tag us.gcr.io/crom-1289/crom-www:v$version .".split(' ')
    workingDir file('docker')
}

task pushAngularContainer(type: Exec) {
    inputs.property('version', version)
    dependsOn buildAngularContainer
    commandLine "gcloud docker -- push us.gcr.io/crom-1289/crom-www:v$version".split(' ')
    workingDir file('docker')
}
